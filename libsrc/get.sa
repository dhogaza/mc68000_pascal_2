GET      IDNT     2,2      Get file routine
         TTL      Get file routine
* NOTICE OF COPYRIGHT AND OWNERSHIP OF SOFTWARE:
*
*  Copyright (C) 1982, 1983, 1984, 1985, 1986, 1987, 1988,
*                1989, 1990 Oregon Software, Inc.
*  All Rights Reserved.
*
*  This program is the property of Oregon Software.  The program or
*  parts of it may be copied and used only as provided under a signed
*  license agreement with Oregon Software.  Any support purchased from
*  Oregon Software does not apply to user-modified programs.  All copies
*  of this program must display this notice and all copyright notices.
*
*
*  Release version: 0045  Level: 1
*  Processor: MC68000
*  System: VERSADOS
*  Subsystem: STANDARD
*
* Get file routine
*
* Last modified by KRIS on 26-Nov-1990 14:06:22
* Purpose:
         IFEQ     1
Update release version for PL-GS0-GS0 at 2.3.0.1
         ENDC
*
* p_get -- get(file)
*
* p_define -- define file pointer
*
* for text files, get(f) means to advance to the next character, while for
* record files, get(f) actually reads the next record.
*
         INCLUDE  LIBDEF
         INCLUDE  LIBMAC

         PROC     P_DEFINE,<F,ADDRESS>
         BEGIN    P_DEFINE,A0/A3/A4/D0/D1
         CHECKIO  F,INPUT
         BTST     #S.TEXT,F.STATUS(A4)       is it a text file?
         BNE.S    DF1               yup
         JSB      P_GETF            nope, read next record
         BRA.S    DF2               and exit
DF1:     JSB      P_GETCH           read one character
DF2:     BSET     #S.DEF,F.STATUS(A4)        buffer is defined
         ENDPR    P_DEFINE

         PROC     P_GET,<F1,ADDRESS>
         BEGIN    P_GET,A0/A3/A4
         CHECKIO  F1,INPUT,GTER
         BTST     #S.DEF,F.STATUS(A4)        buffer already defined ?
         BNE.S    GT1               yes, so exit
         MOVE.L   F1(A6),-(SP)      push file variable
         BSR      P_DEFINE          load the buffer
         ADDQ.L   #4,SP             kill temp on stack
GT1:     BTST     #S.EOF,F.STATUS(A4)        are we at end-of-file?
         BEQ.S    GT2               nope
         ERROR    E.EOF
GT2:     BCLR     #S.DEF,F.STATUS(A4)
GTER:    ENDPR    P_GET
         END
